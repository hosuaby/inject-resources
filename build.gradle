
import org.gradle.api.attributes.java.TargetJvmVersion

plugins {
    id 'base'
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.10.1'
    id 'org.asciidoctor.jvm.convert' version '3.1.0'
}

allprojects {
    group 'io.hosuaby'
    version '0.4.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'

    ext {
        JUNIT_VERSION = System.getProperty('JUNIT_VERSION') ?: '5.7.1'
        JACKSON_VERSION = System.getProperty('JACKSON_VERSION') ?: '2.17.2'
        GSON_VERSION = System.getProperty('GSON_VERSION') ?: '2.11.0'
        SNAKE_YAML_VERSION = System.getProperty('SNAKE_YAML_VERSION') ?: '2.2'
    }
}

configure(subprojects.findAll {it.path != ':inject-resources-junit-vintage'}) {
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

def publishedProjectsPaths = [
        ':inject-resources-core',
        ':inject-resources-commons',
        ':inject-resources-spring',
        ':inject-resources-spring:tests-with-jackson',
        ':inject-resources-spring:tests-with-gson',
        ':inject-resources-junit-jupiter',
        ':inject-resources-junit-jupiter:tests-with-jackson',
        ':inject-resources-junit-jupiter:tests-with-gson',
        ':inject-resources-junit-vintage'
]

def publishedProjects = subprojects.findAll { subproject ->
    publishedProjectsPaths.contains(subproject.path)
}

configure(publishedProjects) {
    def javaVersion = JavaVersion.VERSION_1_8
    Integer javaMajorVersion = Integer.parseInt(javaVersion.getMajorVersion())

    compileJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_11
    }

    configurations {
        runtimeElements {
            attributes {
                attribute TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, javaMajorVersion
            }
        }
        apiElements {
            attributes {
                attribute TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, javaMajorVersion
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

task jacocoMerge(type: JacocoMerge) {
    publishedProjects.each { subproject ->
        executionData subproject.tasks.withType(Test)
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn publishedProjects.jacocoTestReport, project.jacocoMerge

    additionalSourceDirs.from(files(publishedProjects.sourceSets.main.allSource.srcDirs))
    sourceDirectories.from(files(publishedProjects.sourceSets.main.allSource.srcDirs))
    classDirectories.from(files(publishedProjects.sourceSets.main.output))

    executionData(jacocoMerge.destinationFile)

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

coveralls {
    sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

asciidoctor {
    sourceDir project.rootDir
    attributes 'project-version': project.version
    outputDir "docs/${project.version}/asciidoc"
}
