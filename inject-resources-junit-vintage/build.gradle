plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'jacoco'
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jar {
    from project.sourceSets.main.output
    from project(':inject-resources-commons').sourceSets.main.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = '@InjectResources JUnit 4 (Vintage)'
                description = '@InjectResources JUnit 4 extension'
                url = 'https://github.com/hosuaby/inject-resources'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hosuaby'
                        name = 'Alexei KLENIN'
                        email = 'alexei.klenin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:hosuaby/inject-resources.git'
                    developerConnection = 'scm:git:git@github.com:hosuaby/inject-resources.git'
                    url = 'https://github.com/hosuaby/inject-resources'
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = System.getenv('SIGNING_KEY')
    useInMemoryPgpKeys(signingKey, '')
    sign publishing.publications.mavenJava
}

def JACKSON_VERSION = System.getProperty('JACKSON_VERSION') ?: '2.10.5'
def GSON_VERSION = System.getProperty('GSON_VERSION') ?: '2.8.6'
def SNAKE_YAML_VERSION = System.getProperty('SNAKE_YAML_VERSION') ?: '1.28'

dependencies {
    compile project(':inject-resources-core')
    compile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.jetbrains', name: 'annotations', version: '19.0.0'

    compileOnly project(':inject-resources-commons')
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: JACKSON_VERSION
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: JACKSON_VERSION
    compileOnly group: 'com.google.code.gson', name: 'gson', version: GSON_VERSION
    compileOnly group: 'org.yaml', name: 'snakeyaml', version: SNAKE_YAML_VERSION

    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'

    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.14.0'
    testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.4.1'
    testCompile group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    testCompile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: JACKSON_VERSION
    testCompile group: 'io.leangen.geantyref', name: 'geantyref', version: '1.3.11'

    testImplementation project(':test-commons')
    testImplementation project(':inject-resources-core')
    testImplementation project(':inject-resources-commons')
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: JACKSON_VERSION
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: JACKSON_VERSION
    testImplementation group: 'com.google.code.gson', name: 'gson', version: GSON_VERSION
    testImplementation group: 'org.yaml', name: 'snakeyaml', version: SNAKE_YAML_VERSION
}
